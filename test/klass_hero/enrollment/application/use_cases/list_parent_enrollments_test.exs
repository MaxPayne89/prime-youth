defmodule KlassHero.Enrollment.Application.UseCases.ListParentEnrollmentsTest do
  use KlassHero.DataCase, async: true

  import KlassHero.Factory

  alias KlassHero.Enrollment.Application.UseCases.ListParentEnrollments
  alias KlassHero.Enrollment.Domain.Models.Enrollment

  describe "execute/1" do
    test "returns all enrollments for parent" do
      parent = insert(:parent_profile_schema)
      child1 = insert(:child_schema, parent_id: parent.id)
      child2 = insert(:child_schema, parent_id: parent.id)

      enrollment1 = insert(:enrollment_schema, parent_id: parent.id, child_id: child1.id)

      enrollment2 =
        insert(:enrollment_schema, parent_id: parent.id, child_id: child2.id, status: "confirmed")

      _other = insert(:enrollment_schema)

      enrollments = ListParentEnrollments.execute(parent.id)

      assert length(enrollments) == 2
      ids = Enum.map(enrollments, & &1.id)
      assert to_string(enrollment1.id) in ids
      assert to_string(enrollment2.id) in ids
    end

    test "returns domain entities" do
      enrollment_schema = insert(:enrollment_schema)

      [enrollment] = ListParentEnrollments.execute(enrollment_schema.parent_id)

      assert %Enrollment{} = enrollment
      assert is_atom(enrollment.status)
      assert is_binary(enrollment.id)
    end

    test "returns enrollments ordered by enrolled_at descending" do
      parent = insert(:parent_profile_schema)
      child = insert(:child_schema, parent_id: parent.id)

      old =
        insert(:enrollment_schema,
          parent_id: parent.id,
          child_id: child.id,
          enrolled_at: ~U[2025-01-10 10:00:00Z]
        )

      recent =
        insert(:enrollment_schema,
          parent_id: parent.id,
          child_id: child.id,
          enrolled_at: ~U[2025-01-20 10:00:00Z],
          status: "confirmed"
        )

      middle =
        insert(:enrollment_schema,
          parent_id: parent.id,
          child_id: child.id,
          enrolled_at: ~U[2025-01-15 10:00:00Z],
          status: "completed"
        )

      enrollments = ListParentEnrollments.execute(parent.id)

      ids = Enum.map(enrollments, & &1.id)
      assert ids == [to_string(recent.id), to_string(middle.id), to_string(old.id)]
    end

    test "returns empty list when no enrollments" do
      parent = insert(:parent_profile_schema)

      assert ListParentEnrollments.execute(parent.id) == []
    end

    test "returns empty list for non-existent parent" do
      assert ListParentEnrollments.execute(Ecto.UUID.generate()) == []
    end

    test "includes all enrollment statuses" do
      parent = insert(:parent_profile_schema)
      child = insert(:child_schema, parent_id: parent.id)

      insert(:enrollment_schema, parent_id: parent.id, child_id: child.id, status: "pending")
      insert(:enrollment_schema, parent_id: parent.id, child_id: child.id, status: "confirmed")
      insert(:enrollment_schema, parent_id: parent.id, child_id: child.id, status: "completed")
      insert(:enrollment_schema, parent_id: parent.id, child_id: child.id, status: "cancelled")

      enrollments = ListParentEnrollments.execute(parent.id)

      assert length(enrollments) == 4
      statuses = Enum.map(enrollments, & &1.status)
      assert :pending in statuses
      assert :confirmed in statuses
      assert :completed in statuses
      assert :cancelled in statuses
    end
  end
end
