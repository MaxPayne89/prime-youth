defmodule KlassHero.Family.Adapters.Driven.ACL.ChildEnrollmentACL do
  @moduledoc """
  ACL adapter that manages enrollment data for child deletion.

  Queries the `enrollments` and `programs` tables directly to avoid
  a dependency cycle (Enrollment already depends on Family).
  """

  @behaviour KlassHero.Family.Domain.Ports.ForManagingChildEnrollments
  @behaviour KlassHero.Family.Domain.Ports.ForQueryingChildEnrollments

  use KlassHero.Shared.Tracing

  import Ecto.Query, only: [from: 2]

  alias KlassHero.Repo

  @active_statuses ~w(pending confirmed)

  @impl true
  def list_active_with_program_titles(child_id) when is_binary(child_id) do
    span do
      set_attributes("acl",
        source: "family",
        target: "enrollment",
        operation: "list_active_with_program_titles"
      )

      from(e in "enrollments",
        join: p in "programs",
        on: e.program_id == p.id,
        where: e.child_id == type(^child_id, :binary_id),
        where: e.status in ^@active_statuses,
        select: %{
          enrollment_id: type(e.id, :binary_id),
          program_id: type(e.program_id, :binary_id),
          program_title: p.title,
          status: e.status
        }
      )
      |> Repo.all()
    end
  end

  @impl true
  def cancel_active_for_child(child_id) when is_binary(child_id) do
    span do
      set_attributes("acl",
        source: "family",
        target: "enrollment",
        operation: "cancel_active_for_child"
      )

      now = DateTime.utc_now() |> DateTime.truncate(:second)

      {count, _} =
        from(e in "enrollments",
          where: e.child_id == type(^child_id, :binary_id),
          where: e.status in ^@active_statuses
        )
        |> Repo.update_all(
          set: [
            status: "cancelled",
            cancellation_reason: "child_deleted",
            cancelled_at: now,
            updated_at: now
          ]
        )

      {:ok, count}
    end
  end
end
