defmodule KlassHeroWeb.Staff.StaffDashboardLive do
  use KlassHeroWeb, :live_view

  alias KlassHero.Accounts.Scope
  alias KlassHero.Enrollment
  alias KlassHero.Messaging
  alias KlassHero.ProgramCatalog
  alias KlassHero.Provider
  alias KlassHero.Shared.Entitlements
  alias KlassHeroWeb.Theme

  require Logger

  @impl true
  def mount(_params, _session, socket) do
    staff_member = socket.assigns.current_scope.staff_member

    case Provider.get_provider_profile(staff_member.provider_id) do
      {:ok, provider} ->
        all_programs = ProgramCatalog.list_programs_for_provider(staff_member.provider_id)
        programs = Provider.list_assigned_programs(staff_member, all_programs)
        assigned_ids = MapSet.new(programs, & &1.id)

        socket =
          socket
          |> assign(:page_title, gettext("Staff Dashboard"))
          |> assign(:provider, provider)
          |> assign(:staff_member, staff_member)
          |> assign(:assigned_program_ids, assigned_ids)
          |> assign(:programs_empty?, programs == [])
          |> assign(:dual_role?, Scope.dual_role?(socket.assigns.current_scope))
          |> assign(:show_roster, false)
          |> assign(:roster_entries, [])
          |> assign(:roster_program_name, nil)
          |> assign(:roster_program_id, nil)
          |> assign(:can_message?, false)
          |> assign(:roster_enrolled_count, 0)
          |> stream(:programs, programs)

        {:ok, socket}

      {:error, :not_found} ->
        {:ok,
         socket
         |> put_flash(
           :error,
           gettext("The business associated with your account could not be found.")
         )
         |> push_navigate(to: ~p"/")}
    end
  end

  @impl true
  def handle_event("view_roster", %{"id" => program_id} = params, socket) do
    if MapSet.member?(socket.assigns.assigned_program_ids, program_id) do
      roster = Enrollment.list_program_enrollments(program_id)
      can_message? = Entitlements.can_initiate_messaging?(%{provider: socket.assigns.provider})
      enrolled_count = Enum.count(roster, &(&1.status == :confirmed))

      {:noreply,
       assign(socket,
         show_roster: true,
         roster_program_name: Map.get(params, "title", program_id),
         roster_program_id: program_id,
         roster_entries: roster,
         can_message?: can_message?,
         roster_enrolled_count: enrolled_count
       )}
    else
      {:noreply, put_flash(socket, :error, gettext("Unauthorized"))}
    end
  end

  @impl true
  def handle_event("close_roster", _params, socket) do
    {:noreply,
     assign(socket,
       show_roster: false,
       roster_entries: [],
       roster_program_name: nil,
       roster_program_id: nil,
       can_message?: false,
       roster_enrolled_count: 0
     )}
  end

  @impl true
  def handle_event("send_message_to_parent", %{"parent-user-id" => parent_user_id}, socket) do
    if socket.assigns.can_message? do
      provider_id = socket.assigns.provider.id
      roster_entries = socket.assigns.roster_entries
      scope = socket.assigns.current_scope

      valid_confirmed? =
        Enum.any?(roster_entries, fn entry ->
          entry.parent_user_id == parent_user_id and entry.status == :confirmed
        end)

      if valid_confirmed? do
        case Messaging.create_direct_conversation(scope, provider_id, parent_user_id, skip_entitlement_check: true) do
          {:ok, conversation} ->
            {:noreply, push_navigate(socket, to: ~p"/staff/messages/#{conversation.id}")}

          {:error, reason} ->
            Logger.error("Failed to create direct conversation from staff roster",
              reason: inspect(reason),
              provider_id: provider_id,
              parent_user_id: parent_user_id
            )

            {:noreply, put_flash(socket, :error, gettext("Could not start conversation. Please try again."))}
        end
      else
        {:noreply, put_flash(socket, :error, gettext("Cannot message this parent."))}
      end
    else
      {:noreply, put_flash(socket, :error, gettext("Upgrade your plan to send messages."))}
    end
  end

  @impl true
  def render(assigns) do
    ~H"""
    <div id="staff-dashboard" class="max-w-4xl mx-auto px-4 py-6">
      <div id="business-name" class="mb-6">
        <h1 class={Theme.typography(:page_title)}>
          {@provider.business_name}
        </h1>
        <p class={Theme.typography(:body)}>
          {gettext("Welcome, %{name}", name: @staff_member.first_name)}
        </p>
        <.link
          :if={@dual_role?}
          id="cross-nav-provider-link"
          navigate={~p"/provider/dashboard"}
          class="inline-flex items-center gap-1 text-sm text-brand hover:text-brand/80 mt-2"
        >
          {gettext("Manage your business")} →
        </.link>
      </div>

      <div class="mt-8">
        <h2 class={Theme.typography(:section_title)}>
          {gettext("Assigned Programs")}
        </h2>

        <div :if={@programs_empty?} id="programs-empty-state" class="text-center py-8 text-zinc-500">
          {gettext("No programs assigned yet.")}
        </div>

        <div id="assigned-programs" phx-update="stream" class="mt-4 space-y-4">
          <div
            :for={{dom_id, program} <- @streams.programs}
            id={dom_id}
            class="p-4 bg-white rounded-lg shadow-sm border border-zinc-200"
          >
            <h3 class={Theme.typography(:card_title)}>{program.title}</h3>
            <p :if={program.category} class="text-sm text-zinc-500 mt-1">{program.category}</p>

            <div class="flex gap-2 mt-3">
              <.link
                id={"sessions-link-#{program.id}"}
                navigate={~p"/staff/sessions?program_id=#{program.id}"}
                class={[
                  "inline-flex items-center gap-1 px-3 py-1.5 text-sm font-medium",
                  "text-hero-blue-600 bg-hero-blue-50 hover:bg-hero-blue-100",
                  "rounded-md transition-colors"
                ]}
              >
                <.icon name="hero-calendar-days-mini" class="w-4 h-4" />
                {gettext("Sessions")}
              </.link>

              <button
                id={"roster-btn-#{program.id}"}
                phx-click="view_roster"
                phx-value-id={program.id}
                phx-value-title={program.title}
                class={[
                  "inline-flex items-center gap-1 px-3 py-1.5 text-sm font-medium",
                  "text-hero-grey-700 bg-hero-grey-100 hover:bg-hero-grey-200",
                  "rounded-md transition-colors"
                ]}
              >
                <.icon name="hero-user-group-mini" class="w-4 h-4" />
                {gettext("Roster")}
              </button>
            </div>
          </div>
        </div>
      </div>

      <%!-- Staff Roster Modal --%>
      <%= if @show_roster do %>
        <div
          id="staff-roster-backdrop"
          class="fixed inset-0 z-50 bg-black/50"
          phx-click="close_roster"
        >
        </div>
        <div
          id="staff-roster-modal"
          class={[
            "fixed inset-x-4 top-[10%] z-50 mx-auto max-w-lg bg-white",
            "rounded-xl shadow-xl max-h-[80vh] overflow-y-auto"
          ]}
        >
          <div class="flex items-center justify-between p-4 border-b border-hero-grey-200">
            <h2 class="text-lg font-semibold text-hero-charcoal">
              {gettext("Roster: %{name}", name: @roster_program_name)}
            </h2>
            <div class="flex items-center gap-1">
              <%= if @can_message? and @roster_enrolled_count > 0 do %>
                <.link
                  id={"staff-broadcast-#{@roster_program_id}"}
                  navigate={~p"/staff/programs/#{@roster_program_id}/broadcast"}
                  title={gettext("Send Broadcast")}
                  aria-label={gettext("Send Broadcast")}
                  class={[
                    "p-2 rounded-lg transition-colors",
                    "text-hero-grey-400 hover:text-hero-charcoal hover:bg-hero-grey-100"
                  ]}
                >
                  <.icon name="hero-megaphone-mini" class="w-5 h-5" />
                </.link>
              <% else %>
                <button
                  id={"staff-broadcast-#{@roster_program_id}"}
                  type="button"
                  disabled
                  title={
                    if(!@can_message?,
                      do: gettext("Upgrade plan to send broadcasts"),
                      else: gettext("No enrolled parents")
                    )
                  }
                  class="p-2 rounded-lg text-hero-grey-300 cursor-not-allowed"
                >
                  <.icon name="hero-megaphone-mini" class="w-5 h-5" />
                </button>
              <% end %>
              <button
                id="close-roster-btn"
                phx-click="close_roster"
                class="p-1 text-hero-grey-400 hover:text-hero-grey-600"
              >
                <.icon name="hero-x-mark" class="w-5 h-5" />
              </button>
            </div>
          </div>

          <div class="p-4">
            <%= if @roster_entries == [] do %>
              <p class="text-center text-hero-grey-500 py-4">
                {gettext("No enrollments yet.")}
              </p>
            <% else %>
              <ul class="divide-y divide-hero-grey-200">
                <%= for entry <- @roster_entries do %>
                  <li class="py-3 flex items-center justify-between">
                    <div>
                      <span class="font-medium text-hero-charcoal">
                        {Map.get(entry, :child_name, gettext("Unknown"))}
                      </span>
                    </div>
                    <div class="flex items-center gap-2">
                      <span class="text-sm text-hero-grey-500">
                        {Map.get(entry, :status, "")}
                      </span>
                      <%= if @can_message? and entry.status == :confirmed and entry.parent_user_id do %>
                        <button
                          id={"staff-msg-#{entry.enrollment_id}"}
                          type="button"
                          phx-click="send_message_to_parent"
                          phx-value-parent-user-id={entry.parent_user_id}
                          title={gettext("Send Message")}
                          aria-label={gettext("Send Message")}
                          class={[
                            "p-2 inline-flex rounded-lg transition-colors",
                            "text-hero-grey-400 hover:text-hero-charcoal hover:bg-hero-grey-100"
                          ]}
                        >
                          <.icon name="hero-chat-bubble-left-mini" class="w-5 h-5" />
                        </button>
                      <% else %>
                        <button
                          id={"staff-msg-#{entry.enrollment_id}"}
                          type="button"
                          disabled
                          title={staff_message_button_title(@can_message?, entry)}
                          aria-label={staff_message_button_title(@can_message?, entry)}
                          class="p-2 inline-flex rounded-lg text-hero-grey-300 cursor-not-allowed"
                        >
                          <.icon name="hero-chat-bubble-left-mini" class="w-5 h-5" />
                        </button>
                      <% end %>
                    </div>
                  </li>
                <% end %>
              </ul>
            <% end %>
          </div>
        </div>
      <% end %>
    </div>
    """
  end

  defp staff_message_button_title(false = _can_message?, _entry), do: gettext("Upgrade plan to message parents")

  defp staff_message_button_title(true = _can_message?, entry) do
    cond do
      entry.parent_user_id == nil -> gettext("Parent account not available")
      entry.status != :confirmed -> gettext("Enrollment not confirmed")
      true -> gettext("Send Message")
    end
  end
end
